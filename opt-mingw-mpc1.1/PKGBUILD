# Maintainer:  Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov

OPT_MINGW_PREFIX=/opt${MINGW_PREFIX}
if [[ "$MSYSTEM" == "UCRT64" ]]; then
  OPT_MINGW_PACKAGE_PREFIX=opt-ucrt-${MSYSTEM_CARCH}
else
  OPT_MINGW_PACKAGE_PREFIX=opt-mingw-${MSYSTEM_CARCH}
fi

_realname=mpc
pkgbase=mingw-w64-${_realname}1.1
pkgname="${OPT_MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.1.0
pkgrel=1
pkgdesc="Multiple precision complex arithmetic library (mingw-w64)"
url='http://www.multiprecision.org'
license=('LGPL')
arch=('any')
depends=("${OPT_MINGW_PACKAGE_PREFIX}-mpfr")
makedepends=("${OPT_MINGW_PACKAGE_PREFIX}-gmp" "${OPT_MINGW_PACKAGE_PREFIX}-mpfr")
options=('staticlibs' '!emptydirs')
source=(#"http://www.multiprecision.org/mpc/download/${_realname}-${pkgver}.tar.gz"
        https://ftp.gnu.org/gnu/mpc/${_realname}-${pkgver}.tar.gz)
sha256sums=('6985c538143c1208dcb1ac42cedad6ff52e267b47e5f970183a3e75125b43c2e')

build() {
  # Replace the MSys2 default FLAG values
  export LDFLAGS="-pipe"
  export CPPFLAGS=""
  export CFLAGS="-pipe"
  export CXXFLAGS="-pipe"

  local _GCC_VERSION=`${OPT_MINGW_PREFIX}/bin/gcc -dumpversion`
  #echo "_GCC_VERSION := ${_GCC_VERSION}"
  export CC+=" -L${OPT_MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${_GCC_VERSION}"
  export PATH=${OPT_MINGW_PREFIX}/bin:${OPT_MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${_GCC_VERSION}:$PATH

  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
     --build=${MINGW_CHOST} \
    --prefix=${OPT_MINGW_PREFIX} \
    --enable-static \
    --disable-shared \
    --with-gmp=${OPT_MINGW_PREFIX} \
    --with-mpfr=${OPT_MINGW_PREFIX}
  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
