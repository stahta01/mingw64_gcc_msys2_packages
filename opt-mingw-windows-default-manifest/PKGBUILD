# Maintainer:  Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>

OPT_MINGW_PREFIX=/opt${MINGW_PREFIX}
if [[ "$MSYSTEM" == "UCRT64" ]]; then
  OPT_MINGW_PACKAGE_PREFIX=opt-ucrt-${MSYSTEM_CARCH}
else
  OPT_MINGW_PACKAGE_PREFIX=opt-mingw-${MSYSTEM_CARCH}
fi

_realname=windows-default-manifest
pkgbase=opt-mingw-${_realname}
pkgname="${OPT_MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=6.4
pkgrel=1
pkgdesc='Default Windows application manifest (mingw-w64)'
url='https://cygwin.com/'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('BSD')
makedepends=('git' "${OPT_MINGW_PACKAGE_PREFIX}-cc" "${OPT_MINGW_PACKAGE_PREFIX}-autotools")
_GIT_TAG="release-6_4"
source=("git+https://sourceware.org/git/cygwin-apps/${_realname}.git#tag=${_GIT_TAG}")
sha256sums=('SKIP')

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  cp -rf ${srcdir}/${_realname} ${srcdir}/build-${MINGW_CHOST}

  export PATH=${OPT_MINGW_PREFIX}/bin:$PATH

  cd ${srcdir}/build-${MINGW_CHOST}
  ./configure \
    --prefix=${OPT_MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}
  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
}
