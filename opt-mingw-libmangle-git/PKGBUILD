# Maintainer:  Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>

OPT_MINGW_PREFIX=/opt${MINGW_PREFIX}
if [[ "$MSYSTEM" == "UCRT64" ]]; then
  OPT_MINGW_PACKAGE_PREFIX=opt-ucrt-${MSYSTEM_CARCH}
else
  OPT_MINGW_PACKAGE_PREFIX=opt-mingw-${MSYSTEM_CARCH}
fi

_realname=libmangle
pkgbase=opt-mingw-${_realname}-git
pkgname="${OPT_MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${OPT_MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${OPT_MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=10.0.0.r83.gad45df377
pkgrel=1
_commit='ad45df377c697186eac4d6816abfc23175f0c5db'
pkgdesc="MinGW-w64 libmangle"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://mingw-w64.sourceforge.io/"
license=('custom')
groups=("${OPT_MINGW_PACKAGE_PREFIX}-toolchain")
makedepends=("git"
             "${OPT_MINGW_PACKAGE_PREFIX}-cc"
             "${OPT_MINGW_PACKAGE_PREFIX}-binutils"
             "${OPT_MINGW_PACKAGE_PREFIX}-crt-git"
             "${OPT_MINGW_PACKAGE_PREFIX}-headers-git"
             "${OPT_MINGW_PACKAGE_PREFIX}-autotools")
options=('strip' 'staticlibs' '!emptydirs')
source=("mingw-w64"::"git+https://git.code.sf.net/p/mingw-w64/mingw-w64#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/mingw-w64"
  git describe --long ${_commit} | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//g'
}

build() {
  [[ -d ${srcdir}/build-${MSYSTEM} ]] && rm -rf ${srcdir}/build-${MSYSTEM}
  mkdir -p ${srcdir}/build-${MSYSTEM} && cd ${srcdir}/build-${MSYSTEM}

  export PATH=${OPT_MINGW_PREFIX}/bin:$PATH

  ${srcdir}/mingw-w64/mingw-w64-libraries/libmangle/configure \
    --prefix=${OPT_MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST}

  make
}

package() {
  cd ${srcdir}/build-${MSYSTEM}
  make DESTDIR=${pkgdir} install
  install -Dm644 ${srcdir}/mingw-w64/mingw-w64-libraries/libmangle/COPYING ${pkgdir}${OPT_MINGW_PREFIX}/share/licenses/mingw-w64-libraries/COPYING.${_realname}
}
