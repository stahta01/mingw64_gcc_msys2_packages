# Maintainer:  Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov
# Contributor: Martell Malone
# Contributor: Renato Silva

OPT_MINGW_PREFIX=/opt${MINGW_PREFIX}
if [[ "$MSYSTEM" == "UCRT64" ]]; then
  OPT_MINGW_PACKAGE_PREFIX=opt-ucrt-${MSYSTEM_CARCH}
else
  OPT_MINGW_PACKAGE_PREFIX=opt-mingw-${MSYSTEM_CARCH}
fi

_realname=winpthreads
_sourcedir=mingw-w64-git
pkgbase=opt-mingw-${_realname}6-git
pkgname=("${OPT_MINGW_PACKAGE_PREFIX}-${_realname}-git" "${OPT_MINGW_PACKAGE_PREFIX}-libwinpthread-git")
pkgver=6.0.0.5241.e174bf62d33d
pkgrel=1
_commit='e174bf62d33d26b9fee56ef45e8d8566d01a5af7'
pkgdesc="MinGW-w64 winpthreads library"
url="https://mingw-w64.sourceforge.io/"
# The main license of `winpthreads' is MIT, but parts of this library are
# derived from the "POSIX Threads for Microsoft Windows" library, which is
# licensed under BSD [1].
license=('MIT' 'BSD')
arch=('any')
groups=("${OPT_MINGW_PACKAGE_PREFIX}-toolchain")
makedepends=("git"
             "${OPT_MINGW_PACKAGE_PREFIX}-gcc"
             "${OPT_MINGW_PACKAGE_PREFIX}-binutils"
             "${OPT_MINGW_PACKAGE_PREFIX}-crt-git"
             "${OPT_MINGW_PACKAGE_PREFIX}-headers-git")
options=('strip' '!buildflags' 'staticlibs' '!emptydirs' '!debug')
source=("${_sourcedir}"::"git+https://git.code.sf.net/p/mingw-w64/mingw-w64#commit=$_commit"
        "0001-Define-__-de-register_frame_info-in-fake-libgcc_s.patch")
sha256sums=('SKIP'
            'd9e8af81682d9bf70e3d87506f51156cec61260b810a234bce861cb2eb3a5919')

# Declare global variables; begin with underscore to avoid name conflicts
_git_base_commit=

pkgver() {
  cd "${srcdir}/${_sourcedir}"
  local _major=$(head -n 16 mingw-w64-headers/crt/_mingw_mac.h | grep '__MINGW64_VERSION_MAJOR' | sed -e 's/.* //' | tr '\n' '.' | sed 's/.$/\n/')
  local _minor=$(head -n 16 mingw-w64-headers/crt/_mingw_mac.h | grep '__MINGW64_VERSION_MINOR' | sed -e 's/.* //' | tr '\n' '.' | sed 's/.$/\n/')
  local _rev=0
  printf "%s.%s.%s.%s.%s" ${_major} ${_minor} ${_rev} "$(git rev-list --count ${_git_base_commit})" "$(git rev-parse --short=12 ${_git_base_commit})"
}

prepare() {
  cd "${srcdir}/${_sourcedir}"
  _git_base_commit=$(git rev-parse HEAD)

  [[ -f mingw-w64-libraries/winpthreads/src/libgcc/dll_frame_info.c ]] && rm -rf mingw-w64-libraries/winpthreads/src/libgcc/dll_frame_info.c
  git am --committer-date-is-author-date "${srcdir}"/0001-Define-__-de-register_frame_info-in-fake-libgcc_s.patch
  cd "${srcdir}"/${_sourcedir}/mingw-w64-libraries/winpthreads
  autoreconf -vfi
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p "${srcdir}"/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  export PATH=${OPT_MINGW_PREFIX}/bin:$PATH

  declare -a extra_config
  if check_option "debug" "y"; then
    extra_config+=("CFLAGS=-O0 -g -DWINPTHREAD_DBG")
    extra_config+=("CXXFLAGS=-O0 -g -DWINPTHREAD_DBG")
    extra_config+=("CXXFLAGS=")
  fi

  ../${_sourcedir}/mingw-w64-libraries/winpthreads/configure \
    --prefix=${OPT_MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared \
    "${extra_config[@]}"

  # I failed to figure out how to disable stripping, this
  # will have to do instead.
  if check_option "strip" "n"; then
    sed -i 's,^STRIP = .*$,STRIP = true,g'                Makefile
    sed -i 's,^STRIP=.*$,STRIP=true,g'                    libtool
    sed -i 's,old_striplib=.*$strip,old_striplib=true,g'  libtool
    sed -i 's,striplib=.*$,striplib=true,g'               libtool
  fi

  make
}

_install_licenses() {
  # TODO: any more license files?
  install -Dm644 ${srcdir}/${_sourcedir}/COPYING                                 ${pkgdir}${OPT_MINGW_PREFIX}/share/licenses/$1/COPYING
  install -Dm644 ${srcdir}/${_sourcedir}/mingw-w64-libraries/winpthreads/COPYING ${pkgdir}${OPT_MINGW_PREFIX}/share/licenses/$1/mingw-w64-libraries/winpthreads/COPYING
}

package_winpthreads-git() {
  depends=("${OPT_MINGW_PACKAGE_PREFIX}-crt-git" "${OPT_MINGW_PACKAGE_PREFIX}-libwinpthread-git=${pkgver}")
  provides=("${OPT_MINGW_PACKAGE_PREFIX}-${_realname}")
  conflicts=("${OPT_MINGW_PACKAGE_PREFIX}-${_realname}")

  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  mkdir -p ${pkgdir}${OPT_MINGW_PREFIX}/${MINGW_CHOST}
  mv ${pkgdir}${OPT_MINGW_PREFIX}/include ${pkgdir}${OPT_MINGW_PREFIX}/${MINGW_CHOST}/
  mv ${pkgdir}${OPT_MINGW_PREFIX}/lib ${pkgdir}${OPT_MINGW_PREFIX}/${MINGW_CHOST}/
  rm -rf ${pkgdir}${OPT_MINGW_PREFIX}/bin

  _install_licenses ${_realname}
}

package_libwinpthread-git() {
  provides=("${OPT_MINGW_PACKAGE_PREFIX}-libwinpthread")
  conflicts=("${OPT_MINGW_PACKAGE_PREFIX}-libwinpthread")

  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}${OPT_MINGW_PREFIX}/include
  rm -rf ${pkgdir}${OPT_MINGW_PREFIX}/lib

  _install_licenses libwinpthread
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${OPT_MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;

# [1] https://locklessinc.com/articles/pthreads_on_windows/
