# Maintainer:  Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov
# Contributor: Karlson2k (Evgeny Grin)

OPT_MINGW_PREFIX=/opt${MINGW_PREFIX}
if [[ "$MSYSTEM" == "UCRT64" ]]; then
  OPT_MINGW_PACKAGE_PREFIX=opt-ucrt-${MSYSTEM_CARCH}
else
  OPT_MINGW_PACKAGE_PREFIX=opt-mingw-${MSYSTEM_CARCH}
fi

_realname=mpfr
pkgbase=opt-mingw-${_realname}
pkgname=("${OPT_MINGW_PACKAGE_PREFIX}-${_realname}")
_pkgver=4.1.0
pkgver=${_pkgver}.p13
pkgrel=1
pkgdesc="Multiple-precision floating-point library (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=('LGPL')
options=('staticlibs')
depends=("${OPT_MINGW_PACKAGE_PREFIX}-gmp")
makedepends=("${OPT_MINGW_PACKAGE_PREFIX}-cc"
             "${OPT_MINGW_PACKAGE_PREFIX}-gmp"
             "${OPT_MINGW_PACKAGE_PREFIX}-autotools")
source=(https://ftp.gnu.org/gnu/mpfr/${_realname}-${_pkgver}.tar.xz{,.sig}
        patches.diff) # wget https://www.mpfr.org/mpfr-4.1.0/allpatches -O patches.diff
sha256sums=('0c98a3f1732ff6ca4ea690552079da9c597872d30e96ec28414ee23c95558a7f'
            'SKIP'
            '75af77904037442fcd3061a4fee2394194716c542e96989dcfac2445c842daf1')
validpgpkeys=('07F3DBBECC1A39605078094D980C197698C3739D')

prepare() {
  cd "${srcdir}/${_realname}-${_pkgver}"

  patch -p1 -i ${srcdir}/patches.diff
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}

  export PATH=${OPT_MINGW_PREFIX}/bin:$PATH

  ../${_realname}-${_pkgver}/configure \
    --build=${MINGW_CHOST} \
    --prefix=${OPT_MINGW_PREFIX} \
    --enable-static \
    --disable-shared \
    --with-gmp=${OPT_MINGW_PREFIX}

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  rm -f ${pkgdir}${OPT_MINGW_PREFIX}/share/info/dir
}
