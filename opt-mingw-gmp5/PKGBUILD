# Maintainer:  Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov

OPT_MINGW_PREFIX=/opt${MINGW_PREFIX}
if [[ "$MSYSTEM" == "UCRT64" ]]; then
  OPT_MINGW_PACKAGE_PREFIX=opt-ucrt-${MSYSTEM_CARCH}
else
  OPT_MINGW_PACKAGE_PREFIX=opt-mingw-${MSYSTEM_CARCH}
fi

_realname=gmp
pkgbase=opt-mingw-${_realname}5
pkgname=("${OPT_MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=5.1.3
pkgrel=3
pkgdesc="A free library for arbitrary precision arithmetic (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://gmplib.org/"
license=('LGPL3' 'GPL')
makedepends=("${OPT_MINGW_PACKAGE_PREFIX}-autotools" "${OPT_MINGW_PACKAGE_PREFIX}-cc")
source=(https://gmplib.org/download/gmp-${pkgver}/${_realname}-${pkgver}.tar.xz)
options=('staticlibs')
sha256sums=('dee2eda37f4ff541f30019932db0c37f6f77a30ba3609234933b1818f9b07071')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  [[ -d ../stash ]] && rm -rf ../stash
  mkdir ../stash
  cp config.{guess,sub} ../stash
  autoreconf -fiv
  cp -f ../stash/config.{guess,sub} .
}

build() {
  # Replace the MSys2 default FLAG values
  export LDFLAGS="-pipe"
  export CPPFLAGS="-D__USE_MINGW_ANSI_STDIO=0"
  export CFLAGS="-pipe"

  local _GCC_VERSION=`${OPT_MINGW_PREFIX}/bin/gcc -dumpversion`
  #echo "_GCC_VERSION := ${_GCC_VERSION}"
  export CC+=" -L${OPT_MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${_GCC_VERSION}"
  export PATH=${OPT_MINGW_PREFIX}/bin:${OPT_MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${_GCC_VERSION}:$PATH

  echo "PATH := $PATH"
  echo "installation root := $(cygpath -am /)"
  ${CC} --version
  pacman -Qs "${OPT_MINGW_PACKAGE_PREFIX}-toolchain" | grep "${OPT_MINGW_PACKAGE_PREFIX}" || true

  local -a extra_config
  case "$CARCH" in
    i?86|x86_64)
      extra_config+=(--enable-fat)
      ;;
    *)
      extra_config+=(--disable-assembly)
      ;;
  esac
  # Build static version
  mkdir -p "${srcdir}/static-${MINGW_CHOST}" && cd "${srcdir}/static-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --prefix=${OPT_MINGW_PREFIX} \
    --disable-cxx \
    "${extra_config[@]}" \
    --enable-static \
    --disable-shared
  make

  ## Build shared version
  #mkdir -p "${srcdir}/shared-${MINGW_CHOST}" && cd "${srcdir}/shared-${MINGW_CHOST}"
  #../${_realname}-${pkgver}/configure --build=${MINGW_CHOST} \
    #--prefix=${OPT_MINGW_PREFIX} \
    #--disable-cxx \
    #"${extra_config[@]}" \
    #--disable-static \
    #--enable-shared
  #make
}

check() {
  cd "${srcdir}/static-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/static-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  rm -f ${pkgdir}${OPT_MINGW_PREFIX}/share/info/dir

  #cd ${srcdir}/shared-${MINGW_CHOST}
  #mkdir -p ${pkgdir}${OPT_MINGW_PREFIX}/{bin,lib}
  #cp .libs/libgmp*.dll ${pkgdir}${OPT_MINGW_PREFIX}/bin/
  ##cp .libs/libgmp*.dll.a ${pkgdir}${OPT_MINGW_PREFIX}/lib/
}
