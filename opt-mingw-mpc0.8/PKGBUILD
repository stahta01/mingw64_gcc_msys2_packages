# Maintainer:  Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov

OPT_MINGW_PREFIX=/opt${MINGW_PREFIX}
if [[ "$MSYSTEM" == "UCRT64" ]]; then
  OPT_MINGW_PACKAGE_PREFIX=opt-ucrt-${MSYSTEM_CARCH}
else
  OPT_MINGW_PACKAGE_PREFIX=opt-mingw-${MSYSTEM_CARCH}
fi

_realname=mpc
pkgbase=mingw-w64-${_realname}0.8
pkgname="${OPT_MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.8.1
pkgrel=3
pkgdesc="Multiple precision complex arithmetic library (mingw-w64)"
url='http://www.multiprecision.org'
license=('LGPL')
arch=('any')
depends=("${OPT_MINGW_PACKAGE_PREFIX}-mpfr")
makedepends=("${OPT_MINGW_PACKAGE_PREFIX}-gmp" "${OPT_MINGW_PACKAGE_PREFIX}-mpfr")
options=('staticlibs' '!emptydirs' '!strip')
source=(#"http://www.multiprecision.org/mpc/download/${_realname}-${pkgver}.tar.gz"
        #https://ftp.gnu.org/gnu/mpc/${_realname}-${pkgver}.tar.gz
        ftp://gcc.gnu.org/pub/gcc/infrastructure/mpc-${pkgver}.tar.gz)
sha256sums=('e664603757251fd8a352848276497a4c79b7f8b21fd8aedd5cc0598a38fee3e4')

build() {
  # Set good default FLAGS and add static-libgcc flag
  export LDFLAGS="-pipe"
  export CPPFLAGS=""
  export CFLAGS="-pipe -static-libgcc"
  export CXXFLAGS="-pipe"

  export PATH=${OPT_MINGW_PREFIX}/bin:$PATH

  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
     --build=${MINGW_CHOST} \
    --prefix=${OPT_MINGW_PREFIX} \
    --enable-static \
    --disable-shared \
    --with-gmp=${OPT_MINGW_PREFIX} \
    --with-mpfr=${OPT_MINGW_PREFIX}
  make
}

check() {
  # Set good default FLAGS and add static-libgcc flag
  export LDFLAGS="-pipe"
  export CPPFLAGS=""
  export CFLAGS="-pipe -static-libgcc"
  export CXXFLAGS="-pipe"

  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
