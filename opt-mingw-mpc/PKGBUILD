# Maintainer:  Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov <Alexpux@gmail.com>

OPT_MINGW_PREFIX=/opt${MINGW_PREFIX}
if [[ "$MSYSTEM" == "UCRT64" ]]; then
  OPT_MINGW_PACKAGE_PREFIX=opt-ucrt-${MSYSTEM_CARCH}
else
  OPT_MINGW_PACKAGE_PREFIX=opt-mingw-${MSYSTEM_CARCH}
fi

_realname=mpc
pkgbase=opt-mingw-${_realname}
pkgname="${OPT_MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.2.1
pkgrel=1
pkgdesc="Multiple precision complex arithmetic library (mingw-w64)"
url='http://www.multiprecision.org'
license=('LGPL')
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
depends=("${OPT_MINGW_PACKAGE_PREFIX}-gmp")
makedepends=("${OPT_MINGW_PACKAGE_PREFIX}-gmp" "${OPT_MINGW_PACKAGE_PREFIX}-mpfr" "${OPT_MINGW_PACKAGE_PREFIX}-autotools")
options=('staticlibs' '!emptydirs')
source=(#"http://www.multiprecision.org/mpc/download/${_realname}-${pkgver}.tar.gz"
        https://ftp.gnu.org/gnu/mpc/${_realname}-${pkgver}.tar.gz)
sha256sums=('17503d2c395dfcf106b622dc142683c1199431d095367c6aacba6eec30340459')

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  export PATH=${OPT_MINGW_PREFIX}/bin:$PATH

  ../${_realname}-${pkgver}/configure \
     --build=${MINGW_CHOST} \
    --prefix=${OPT_MINGW_PREFIX} \
    --enable-static \
    --disable-shared \
    --with-gmp=${OPT_MINGW_PREFIX} \
    --with-mpfr=${OPT_MINGW_PREFIX}
  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check || true
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  rm -f ${pkgdir}${OPT_MINGW_PREFIX}/share/info/dir
}
